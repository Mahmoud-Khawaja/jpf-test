plugins {
    id 'java'
}

group = 'gov.nasa'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def jpfCoreHome = System.getProperty("user.home") + "/Downloads/nas/jpf-core"

dependencies {
    implementation files("${jpfCoreHome}/build/jpf.jar")
    implementation files("${jpfCoreHome}/build/jpf-annotations.jar")
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/peers']
        }
    }

    classes {
        java {
            srcDirs = ['src/classes/modules']
        }
    }

    examples {
        java {
            srcDirs = ['src/examples']
        }
    }
}

// compile model classes with --patch-module
tasks.register('compileClasses', JavaCompile) {
    source = fileTree(dir: 'src/classes/modules')
    classpath = files()
    destinationDirectory = file("${buildDir}/classes/modules")
    
    sourceCompatibility = '11'
    targetCompatibility = '11'
    
    options.compilerArgs = [
            '--module-source-path', file('src/classes/modules').absolutePath,
            '--add-reads', 'java.base=ALL-UNNAMED',
            '--patch-module', "java.base=" + file('src/classes/modules/java.base').absolutePath
    ]
}

tasks.register('compileExamples', JavaCompile) {
    source = sourceSets.examples.java
    classpath = files()
    destinationDirectory = file("${buildDir}/examples")
    dependsOn compileClasses

    sourceCompatibility = '11'
    targetCompatibility = '11'
}

tasks.register('createJpfTestJar', Jar) {
    archiveFileName = 'jpf-test.jar'
    destinationDirectory = file("${buildDir}")
    from sourceSets.main.output
}

// jar with model classes (String)
tasks.register('createJpfTestClassesJar', Jar) {
    archiveFileName = 'jpf-test-classes.jar'
    destinationDirectory = file("${buildDir}")
    dependsOn compileClasses
    
    //  module prefix (java.base/)
    from("${buildDir}/classes/modules") {
        include '**/*.class'
    }
}

tasks.register('createExamplesJar', Jar) {
    archiveFileName = 'jpf-test-examples.jar'
    destinationDirectory = file("${buildDir}")
    from "${buildDir}/examples"
    dependsOn compileExamples
}

tasks.register('buildJars') {
    group = 'JPF Build'
    description = 'Builds all JPF test extension JARs'
    dependsOn createJpfTestJar
    dependsOn createJpfTestClassesJar
    dependsOn createExamplesJar
}

build.dependsOn buildJars

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
